plugins {
    id 'java'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'net.minecraftforge.gradleutils' version '1.+'
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

group = 'net.minecraftforge.lex' // Repackaged from 'org.cadixdev' to publish on Forge's Maven with out transformers
archivesBaseName = project.name.toLowerCase()
version = gradleutils.getTagOffsetVersion()

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.cadixdev:atlas:$atlasVersion"
    implementation "org.cadixdev:lorenz:$lorenzVersion"
    implementation "org.cadixdev:lorenz-asm:$lorenzVersion"
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.ow2.asm:asm-commons:9.1'
    implementation 'com.google.jimfs:jimfs:1.2'
}

processResources {
    from 'LICENSE.txt'
}

shadowJar {
  mergeServiceFiles()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': name,
                'Implementation-Version': version,
                'Main-Class': 'org.cadixdev.vignette.VignetteMain'
        )
    }
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives shadowJar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    apply plugin: 'signing'
    signing {
        required { !version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(tasks.uploadArchives) }
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Maven Central
            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }
            
            // Forge's maven
            if (System.env.MAVEN_USER) {
                repository(url: 'https://maven.minecraftforge.net/') {
                    authentication(
                        userName: System.env.MAVEN_USER ?: 'not',
                        password: System.env.MAVEN_PASSWORD ?: 'set'
                    )
                }
            } else {
                repository(url: 'file://' + rootProject.file('repo').getAbsolutePath())
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    packaging = 'jar'
                    url = project.url
                    inceptionYear = project.inceptionYear

                    scm {
                        url = 'https://github.com/CadixDev/Vignette'
                        connection = 'scm:git:https://github.com/CadixDev/Vignette.git'
                        developerConnection = 'scm:git:git@github.com:CadixDev/Vignette.git'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/CadixDev/Vignette/issues'
                    }

                    licenses {
                        license {
                            name = 'Mozilla Public License 2.0'
                            url = 'https://opensource.org/licenses/MPL-2.0'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'jamierocks'
                            name = 'Jamie Mansfield'
                            email = 'jmansfield@cadixdev.org'
                            url = 'https://www.jamiemansfield.me/'
                            timezone = 'Europe/London'
                        }
                    }
                }
            }
        }
    }
}
